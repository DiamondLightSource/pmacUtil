CLOSE

;###############################################
; Define motion of a 3 jack system with a mirror on top
; Look at 3jack-mirror-help.edl for a picture
; Original Author: Tom Cobb
; Used variables: Q71..Q79, Q81..Q89, Q91..Q99, Q100..
; Macros (and example values): 
; COORD = $(COORD) ;CS number (only works for CS 1..9), e.g. 2
; PLC = $(PLC) ;PLC number, should be CS number+15, e.g. 17
; J1 = $(J1) ;Axisnum for jack 1, e.g. 2
; J1X = $(J1X) ;global X co-ord of J1 base in mm, e.g. 0
; J1Z = $(J1Z) ;global Z co-ord of J1 base in mm, e.g. -100
; J2 = $(J2) ;Axisnum for jack 2, e.g. 3
; J2X = $(J2X) ;global X co-ord of J2 base in mm, e.g. 50
; J2Z = $(J2Z) ;global Z co-ord of J2 base in mm, e.g. 100
; J3 = $(J3) ;Axisnum for jack 3, e.g. 4
; J3X = $(J3X) ;global X co-ord of J3 base in mm, e.g. -50
; J3Z = $(J3Z) ;global Z co-ord of J3 base in mm, e.g. 100
; JTX = $(JTX) ;jack plane X co-ord of J1 top in mm, e.g. 20
; JTZ = $(JTZ) ;jack plane Z co-ord of J1 top in mm, e.g. 20
; MP = $(MP) ;pitch of mirror support in deg, e.g. 45
; MR = $(MR) ;roll of mirror support in deg, e.g. 45
; MD = $(MD) ;depth of actual mirror in mm, e.g. 10
; MCX = $(MCX) ;global X co-ord of measure point in mm, e.g. 0
; MCZ = $(MCZ) ;global Z co-ord of measure in mm, e.g. 10
;################################################

; Note that we are providing a CS axis with an MRES of 0.0001 and EGU in mm
; This is needed as the motor record will only move in full steps

; Change to CS$(COORD)
&$(COORD)

; Set relevant axes to use kinematics
#$(J1)->I
#$(J2)->I
#$(J3)->I

; Trig in degrees
i15=0

; These are set by mres_reporting_motor.template
#define J1MRES P(100+$(J1))
#define J1OFF  P(300+$(J1))
#define J2MRES P(100+$(J2))
#define J2OFF  P(300+$(J2))
#define J3MRES P(100+$(J3))
#define J3OFF  P(300+$(J3))

; Setup the inputs, all are in degrees or mm
; These are the global co-ordinates of J1
#define J1X Q100
J1X = $(J1X)
#define J1Z Q101
J1Z = $(J1Z)
; These are the global co-ordinates of J2
#define J2X Q102
J2X = $(J2X)
#define J2Z Q103
J2Z = $(J2Z)
; These are the global co-ordinates of J3
#define J3X Q104
J3X = $(J3X)
#define J3Z Q105
J3Z = $(J3Z)
; These are the jack plane co-ordinates of J1
; The mirror support is mounted at (0,0) relative to these co-ordinates
#define JTX Q106
JTX = $(JTX)
#define JTZ Q107
JTZ = $(JTZ)
; This is the pitch of the mirror support mounted on the jack plane
#define MP  Q108
MP = $(MP)
; This is the roll of the mirror support
#define MR  Q109
MR = $(MR)
; This is the mirror depth
#define MD  Q110
MD = $(MD)
; This is the measure point for pitch, roll, Y in global co-ordinates
#define MCX Q111
MCX = $(MCX)
#define MCZ Q112
MCZ = $(MCZ)

; These are some general purpose vectors for use in the kinematic calculations
#define J1Y Q113
#define J2Y Q114
#define J3Y Q115
#define JP  Q116
#define JR  Q117
#define TP  Q118
#define TR  Q119
#define UX  Q120
#define UY  Q121
#define UZ  Q122
#define VX  Q123
#define VY  Q124
#define VZ  Q125
#define WX  Q126
#define WY  Q127
#define WZ  Q128

OPEN FORWARD
; This kinematic translates jacks into pitch, roll, y
; Q7 = PITCH in degrees
; Q8 = ROLL in degrees
; Q9 = Y in mm
CLEAR
	; First we get the jack heights and put them in the correct variables
	J1Y = J1MRES*P$(J1)+J1OFF
	J2Y = J2MRES*P$(J2)+J2OFF
	J3Y = J3MRES*P$(J3)+J3OFF
	; Next we calculate vectors U = J3 - J1, V = J2 - J1
	; note that J3X must be > J2X
	UX = J3X - J1X
	UY = J3Y - J1Y
	UZ = J3Z - J1Z
	VX = J2X - J1X
	VY = J2Y - J1Y
	VZ = J2Z - J1Z
	; The cross product of these gives W, the normal to the jack plane
	WX = UY*VZ-UZ*VY
	WY = UZ*VX-UX*VZ
	WZ = UX*VY-UY*VX
	; We can calculate the pitch and roll of this normal from the Y+ vector
	; WY != 0 as jacks are all in Y direction
	JP = atan(WZ/WY)
	JR = -atan(WX/WY)	
	TP = JP + MP
	TR = JR + MR
	; Add the pitch and roll of the mirror support in and we have pitch and roll
	Q7 = TP*10000	
	Q8 = TR*10000
	; Now we work out Y
	; First we calculate U, mirror plane normal
    UX = -tan(TR)
    UY = 1.0
    UZ = tan(TP)	
	; Now work out V, the origin of the jack plane
	; Start at the top of jack1, then move to the origin of the jack plane
	; Then up by the depth of the mirror plane
	VX = J1X - cos(JR)*JTX
	VY = J1Y + sin(JP)*JTZ - sin(JR)*JTX + MD/(cos(TR)*cos(TP))
	VZ = J1Z - cos(JP)*JTZ
	; Now we can calculate the equation of the mirror plane, and so Y
	Q9 = (UX*(VX-MCX)/UY + VY + UZ*(VZ-MCZ)/UY)*10000
CLOSE
	
OPEN INVERSE
; This kinematic translates pitch, roll, y into jacks
; P$(J1) = J1 in mm
; P$(J2) = J2 in mm
; P$(J3) = J3 in mm
CLEAR
	; get the total pitch and roll
	TP = Q7 * 0.0001
	TR = Q8 * 0.0001
	JP = TP - MP
	JR = TR - MR
	; define U, normal to the mirror plane
	UX = -tan(TR)
	UY = 1.0
	UZ = tan(TP)
	; define V, the measurement point on the mirror plane, moved down by the depth of the mirror
	VX = MCX
	VY = Q9 * 0.0001 - MD/(cos(TR)*cos(TP))
	VZ = MCZ
	; Now work out W, the origin of the jack plane
	; Start at the top of jack1, then move to the origin of the jack plane
	WX = J1X - cos(JR)*JTX
	WZ = J1Z - cos(JP)*JTZ
	; work out VY using the equation of the lower mirror plane
	WY = UX*(VX-WX)/UY + VY + UZ*(VZ-WZ)/UY
	; define V, normal to jack plane
	VX = -tan(JR)
	VY = 1.0
	VZ = tan(JP)
	; we now have W, a point on the jack plane, and a V, the normal, so use equation to work out jack heights
	P$(J1) = (VX*(WX-J1X)/VY + WY + VZ*(WZ-J1Z)/VY - J1OFF) / J1MRES
	P$(J2) = (VX*(WX-J2X)/VY + WY + VZ*(WZ-J2Z)/VY - J2OFF) / J2MRES
	P$(J3) = (VX*(WX-J3X)/VY + WY + VZ*(WZ-J3Z)/VY - J3OFF) / J3MRES
CLOSE	

; These are some general purpose vectors for use in the PLC calculations
#define J1Y Q129
#define J2Y Q130
#define J3Y Q131
#define JP  Q132
#define JR  Q133
#define UX  Q136
#define UY  Q137
#define UZ  Q138
#define VX  Q139
#define VY  Q140
#define VZ  Q141
#define WX  Q142
#define WY  Q143
#define WZ  Q144
#define timer i(5111+($(PLC)&30)*50+$(PLC)%2)
#define MilliSeconds * 8388608/i10

OPEN PLC $(PLC)
; PLC for position reporting
CLEAR
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define J1VAL m$(J1)62/(I$(J1)08*32)
#define J2VAL m$(J2)62/(I$(J2)08*32)
#define J3VAL m$(J3)62/(I$(J3)08*32)
; Put deadband into Q9x
#define J1DB  i$(J1)65*J1MRES/16
#define J2DB  i$(J2)65*J2MRES/16
#define J3DB  i$(J3)65*J3MRES/16
ADDRESS&$(COORD)
	; First we get the jack heights and put them in the correct variables
	J1Y = J1MRES*J1VAL+J1OFF
	J2Y = J2MRES*J2VAL+J2OFF
	J3Y = J3MRES*J3VAL+J3OFF
	; Next we calculate vectors U = J3 - J1, V = J2 - J1
	; note that J3X must be > J2X
	UX = J3X - J1X
	UY = J3Y - J1Y
	UZ = J3Z - J1Z
	VX = J2X - J1X
	VY = J2Y - J1Y
	VZ = J2Z - J1Z
	; The cross product of these gives W, the normal to the jack plane
	WX = UY*VZ-UZ*VY
	WY = UZ*VX-UX*VZ
	WZ = UX*VY-UY*VX
	; We can calculate the pitch and roll of this normal from the Y+ vector
	; WY != 0 as jacks are all in Y direction
	JP = atan(WZ/WY)
	JR = -atan(WX/WY)	
	TP = JP + MP
	TR = JR + MR
	; Add the pitch and roll of the mirror support in and we have pitch and roll
	Q87 = TP*10000	
	Q88 = TR*10000
	; Now we work out Y
	; First we calculate U, mirror plane normal
    UX = -tan(TR)
    UY = 1.0
    UZ = tan(TP)	
	; Now work out V, the origin of the jack plane
	; Start at the top of jack1, then move to the origin of the jack plane
	; Then up by the depth of the mirror plane
	VX = J1X - cos(JR)*JTX
	VY = J1Y + sin(JP)*JTZ - sin(JR)*JTX + MD/(cos(TR)*cos(TP))
	VZ = J1Z - cos(JP)*JTZ
	; Now we can calculate the equation of the mirror plane, and so Y
	Q89 = (UX*(VX-MCX)/UY + VY + UZ*(VZ-MCZ)/UY)*10000
	; Now calculate deadbands
    Q97=ABS(J1DB)*10000
    Q98=ABS(J1DB)*10000
    Q99=ABS(J1DB)*10000
CLOSE
ENABLE PLC $(PLC)


