CLOSE

;##################################################################
; Define motion for 2 slit blades driven by one axis
; that moves them both to adjust the position,
; and another that moves only one to adjust the gap.
;
; Original Author: Emma Shepherd
; Used variables: Q71..Q79, Q81..Q89, Q91..Q99
; Macros
; COORD = $(COORD) ;CS number, e.g. 2
; PLC = $(PLC) ;PLC number, should be CS number+15, e.g. 17
; CENT = $(CENT) ;Axisnum for centre motor, e.g. 1
; GAP = $(GAP) ;Axisnum for gap motor, e.g. 3
; DIR = $(DIR) ;Set to 1 if aperture is opened by driving positively,
; and -1 if negatively. 
;##################################################################

; Note that we are providing a CS axis with an MRES of 0.001 and EGU the same
; as the underlying axes
; This is needed as the motor record will only move in full steps

; Change to CS$(COORD)
&$(COORD)

; Set relevant axes to use kinematics
#$(CENT)->I ; centre motor
#$(GAP)->I  ; gap motor

; These are set by mres_reporting_motor.template
#define CENTMRES P(100+$(CENT))
#define CENTOFF  P(300+$(CENT))
#define GAPMRES P(100+$(GAP))
#define GAPOFF  P(300+$(GAP))

OPEN FORWARD
; Calculate tool-tip centre position (gap is obtained directly from motor readback)
; X = Q7 = centre in mm = {CENT}+{GAP}/2
; Y = Q8 = gap in mm = $(DIR)*{GAP} 
; Note that we divide by 0.0001 to give CS mres of 0.0001
; Also note the use of mres and offset in the calculation
CLEAR
Q7=(CENTMRES*P$(CENT)+CENTOFF+(GAPMRES*P$(GAP)+GAPOFF)/2)/0.0001
Q8=$(DIR)*(GAPMRES*P$(GAP)+GAPOFF)/0.0001
CLOSE

OPEN INVERSE
; Calculate centre motor position from tool-tip co-ordinates
; P$(CENT) = {centre}-$(DIR)*{gap}/2
; P$(GAP)  = $(DIR)*{gap}
CLEAR
P$(CENT)=(Q7*0.0001-$(DIR)*(Q8*0.00005)-CENTOFF)/CENTMRES
P$(GAP)=($(DIR)*(Q8*0.0001)-GAPOFF)/GAPMRES
CLOSE

; A PLC(sx+15) needs to be made to do position reporting
; Readbacks should be in &{axisnum}Q81..89 
OPEN PLC$(PLC)
CLEAR
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define CENTVAL m$(CENT)62/(I$(CENT)08*32)
#define GAPVAL m$(GAP)62/(I$(GAP)08*32)
; Put deadband into Q9x
#define GAPDB  i$(GAP)65*GAPMRES/16
#define CENTDB  i$(CENT)65*CENTMRES/16
ADDRESS&$(COORD)
Q87=(CENTMRES*CENTVAL+CENTOFF+(GAPMRES*GAPVAL+GAPOFF)/2)/0.0001
Q88=$(DIR)*(GAPMRES*GAPVAL+GAPOFF)/0.0001
Q97=(ABS(GAPDB) + ABS(CENTDB))*2/0.0001
Q98=Q97
CLOSE
ENABLE PLC$(PLC)
